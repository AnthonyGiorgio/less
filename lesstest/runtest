#!/usr/bin/perl
use strict;

# Run one or more test files.
my $usage = "usage: run [-d lesstest-dir] [-l less.exe] [-r temp-dir] [-s lt_screen] [-t lesstest] [file.lt | dir]...\n";

use Getopt::Std;
use Cwd;

my $rundir;
my $lesstest;
my $lt_screen;
my $less;

exit main();
sub main {
	my %opt;
	die $usage if not getopts('d:l:r:s:t:', \%opt);
	die $usage if not @ARGV;

	my $cwd = getcwd();
	my $srcdir = ($opt{d} or $cwd);
	$rundir     = (rfile($opt{r}, $cwd) or "$srcdir/.runtest_dir");
	$lesstest   = (rfile($opt{t}, $cwd) or "$srcdir/lesstest");
	$lt_screen  = (rfile($opt{s}, $cwd) or "$srcdir/lt_screen");
	$less       = (rfile($opt{l}, $cwd) or "$srcdir/../obj/less");
	die "cannot execute $lesstest"   if not -x $lesstest;
	die "cannot execute $lt_screen"  if not -x $lt_screen;
	die "cannot execute $less"       if not -x $less;
	die "cannot create $rundir" if system "rm -rf '$rundir' && mkdir -p '$rundir'";
	die "cannot chdir to $rundir: $!" if not chdir $rundir;

	my $errs = 0;
	foreach my $file (@ARGV) {
		$errs += run(rfile($file, $cwd));
	}
	system "rm -rf '$rundir'";
	if ($errs > 0) {
		print "ERRS $errs errors\n";
		return 1;
	}
	return 0;
}

# Run a xxx.lt file.
sub run {
	my ($file) = @_;
	if (-d $file) {
		return run_dir($file);
	}
	if ($file !~ /\.lt$/) {
		print "SKIP unknown file suffix: $file\n";
		return 0;
	}
	if (not -f $file) {
		print "ERR  cannot open $file\n";
		return 1;
	}
	print "TEST $file\n";
	my $cmd = "$lesstest -s '$lt_screen' -t '$file' '$less'";
	my $err = system $cmd;
	if ($err) {
		print "ERR  status $err from $cmd\n";
		return 1;
	}
	return 0;
}

sub run_dir {
	my ($dir) = @_;
	my $errs = 0;
	my $dd;
	if (not opendir($dd, $dir)) {
		print "ERR  cannot open directory $dir: $!\n";
		return 1;
	}
	while (my $entry = readdir($dd)) {
		next if $entry =~ /^\./;
		$errs += run("$dir/$entry");
	}
	closedir $dd;
	return $errs;
}

sub rfile {
	my ($file, $cwd) = @_;
	return undef if not defined $file;
	$file = "$cwd/$file" unless $file =~ m|^/|;
	return $file;
}
