#!/usr/bin/perl
use strict;

# Run one or more test files.

my $usage = "usage: run [-l less.exe] [-r temp_dir] [-o lesstest-opts] [file.lt | dir]...\n";

use Getopt::Std;
use Cwd;

my $testdir   = ".";
my $rundir    = ".runtest_dir";
my $lesstest  = "$testdir/lesstest";
my $less      = "$testdir/../obj/less";
my $lesstest_opts = "";
my %opt;

exit (main() ? 0 : 1);
sub main {
	die $usage if not getopts('l:o:r:v', \%opt);
	die $usage if not @ARGV;
	$less = $opt{l} if $opt{l};
	$lesstest_opts = $opt{o} if $opt{o};
	$lesstest_opts =~ s/^\s*//;
	$lesstest_opts =~ s/\s*$//;
	$lesstest_opts = '-'.$lesstest_opts if $lesstest_opts and $lesstest_opts !~ /^-/;
	my $odir = getcwd();
	$rundir = $opt{r} if $opt{r};
	system "rm -rf $rundir ; mkdir -p $rundir";
	die "cannot chdir to $rundir: $!" if not chdir $rundir;

	my $errs = 0;
	foreach my $file (@ARGV) {
		$file = "$odir/$file" unless $file =~ m|^/|;
		$errs += run($file);
	}
	if ($errs > 0) {
		print "ERRS $errs errors\n";
		return 0;
	}
	return 1;
}

# Run a xxx.lt file.
sub run {
	my ($file) = @_;
	if (-d $file) {
		return run_dir($file);
	}
	if ($file !~ /\.lt$/) {
		print "SKIP unknown file suffix: $file\n";
		return 0;
	}
	if (not -f $file) {
		print "ERR  cannot open $file\n";
		return 1;
	}
	##print "FILE $file\n";
	my $cmd = "$lesstest -s $testdir/lt_screen -t $file $lesstest_opts $less";
	print "RUN  $cmd\n" if $opt{v};
	my $err = system $cmd;
	if ($err) {
		print "ERR  status $err from $cmd\n";
		return 1;
	}
	return 0;
}

sub run_dir {
	my ($dir) = @_;
	my $errs = 0;
	my $dd;
	if (not opendir($dd, $dir)) {
		print "ERR  cannot open directory $dir: $!\n";
		return 1;
	}
	while (my $entry = readdir($dd)) {
		next if $entry =~ /^\./;
		$errs += run("$dir/$entry");
	}
	closedir $dd;
	return $errs;
}
